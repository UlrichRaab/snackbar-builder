apply plugin: 'com.android.library'
apply plugin: 'com.github.dcendents.android-maven'
apply plugin: 'com.jfrog.bintray'

android {
    compileSdkVersion 23
    buildToolsVersion "23.0.3"

    defaultConfig {
        minSdkVersion 16
        targetSdkVersion 23
        versionCode 1
        versionName "1.0.0"
    }
    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }
}

Properties properties = new Properties()
properties.load(project.rootProject.file('local.properties').newDataInputStream())

bintray {

    user = properties.getProperty("bintray.user")
    key = properties.getProperty("bintray.apikey")

    configurations = ['archives'] //When uploading configuration files
    pkg {
        repo = 'maven'
        name = "snackbar-builder"
        desc = 'Builder for snackbars'
        websiteUrl = "https://github.com/UlrichRaab/snackbar-builder"
        issueTrackerUrl = 'https://github.com/UlrichRaab/snackbar-builder/issues'
        vcsUrl = "https://github.com/UlrichRaab/snackbar-builder.git"
        licenses = ['Apache-2.0']
        labels = ['Android', 'Snackbar']
        publicDownloadNumbers = true
    }
}

install {
    repositories.mavenInstaller {
        pom {
            // noinspection GroovyAssignabilityCheck
            project {
                packaging 'aar'
                groupId "de.ulrichraab"
                artifactId "snackbar-builder"
                // noinspection GroovyAssignabilityCheck
                name 'Builder for snackbars'
                url "https://github.com/UlrichRaab/snackbar-builder"
                licenses {
                    license {
                        // noinspection GroovyAssignabilityCheck
                        name 'The Apache Software License, Version 2.0'
                        url 'http://www.apache.org/licenses/LICENSE-2.0.txt'
                    }
                }
                developers {
                    developer {
                        id 'ulrichraab'
                        // noinspection GroovyAssignabilityCheck
                        name 'Ulrich Raab'
                        email 'ulraab@gmail.com'
                    }
                }
                scm {
                    connection "https://github.com/UlrichRaab/snackbar-builder.git"
                    developerConnection "https://github.com/UlrichRaab/snackbar-builder.git"
                    url "https://github.com/UlrichRaab/snackbar-builder"

                }
            }
        }
    }
}

dependencies {
    compile 'com.android.support:design:23.3.0'
    // Testing
    testCompile 'junit:junit:4.12'
}

task sourcesJar(type: Jar) {
    // noinspection GroovyAssignabilityCheck
    from android.sourceSets.main.java.srcDirs
    classifier = 'sources'
}

task javadoc(type: Javadoc) {
    source = android.sourceSets.main.java.srcDirs
    // noinspection GroovyAssignabilityCheck
    classpath += project.files(android.getBootClasspath().join(File.pathSeparator))
    failOnError false
}

task javadocJar(type: Jar, dependsOn: javadoc) {
    classifier = 'javadoc'
    // noinspection GroovyAccessibility
    from javadoc.destinationDir
}

artifacts {
    archives javadocJar
    archives sourcesJar
}
